#@TYPE: Machine
#@NAME: QEMU ZynqMP CG machine
#@DESCRIPTION: Machine configuration for running a ZynqMP CG system on QEMU w/ testimage

# This machine is NOT designed to be inherited by other machines or used as an
# example of how to create a machine.  It is only useful for running testimage
# with runqemu.

#### Preamble
MACHINEOVERRIDES =. "${@['', 'qemu-zynqmp-cg:']['qemu-zynqmp-cg' !='${MACHINE}']}"
#### Regular settings follow

# The following is from conf/machine/include/qemu.inc, but we can not use it
# as it changes other values that need to come from the distro and the
# AMD machine settings
XSERVER ?= "xserver-xorg \
            ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast xserver-xorg-extension-glx', '', d)} \
            xf86-video-fbdev \
            xf86-video-modesetting \
            "

MACHINE_FEATURES = "alsa bluetooth usbgadget screen vfat"

MACHINEOVERRIDES =. "qemuall:"

IMAGE_FSTYPES += "tar.bz2 ext4"

# Don't include kernels in standard images
RDEPENDS:${KERNEL_PACKAGE_NAME}-base = ""

# Provide the nfs server kernel module for all qemu images
KERNEL_FEATURES:append:pn-linux-yocto = " features/nfsd/nfsd-enable.scc"
KERNEL_FEATURES:append:pn-linux-yocto-rt = " features/nfsd/nfsd-enable.scc"
KERNEL_FEATURES:append:pn-linux-xlnx = " features/nfsd/nfsd-enable.scc"


# Now include the generic machine which already supports QEMU booting
require conf/machine/zynqmp-cg-generic.conf

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' qemu_zynqmp_cg']['qemu-zynqmp-cg' != '${MACHINE}']}"
